// the status bar of Soundless Mound
// not a lot of effort was put into it
Class mkStatBar : BaseStatusBar
{
	HUDFont mBaseFont, mTinyFont, mSmallFont;
	DynamicValueInterpolator HealthInter;
	Inventory LastSelected;

	override void Init()
	{
		Super.Init();
		Font f = "HWFONTSM";
		mBaseFont = HUDFont.Create(f);
		f = "HWFONTTY";
		mTinyFont = HUDFont.Create(f);
		mSmallFont = HUDFont.Create(SmallFont);
		SetSize(0,320,200);
		HealthInter = DynamicValueInterpolator.Create(0,0.25,1,4);
		LastSelected = null;
	}

	override void Draw( int state, double TicFrac )
	{
		Super.Draw(state,TicFrac);
		if ( (state != HUD_StatusBar) && (state != HUD_Fullscreen) )
			return;
		BeginHUD();
		int CurHealth = HealthInter.GetValue();
		int hcol = Font.CR_GREEN;
		if ( CurHealth < 50 ) hcol = Font.CR_GOLD;
		else if ( CurHealth < 25 ) hcol = Font.CR_RED;
		DrawString(mBaseFont,FormatNumber(CurHealth,3),(32,-16),DI_TEXT_ALIGN_RIGHT,hcol);
		DrawImage("hpicon",(36,-16),DI_ITEM_LEFT_TOP);
		Inventory CurAmmo = GetCurrentAmmo();
		if ( CurAmmo )
		{
			DrawString(mBaseFont,FormatNumber(CurAmmo.Amount,3),(32,-28),DI_TEXT_ALIGN_RIGHT,Font.CR_WHITE);
			DrawImage("amicon",(36,-28),DI_ITEM_LEFT_TOP);
		}
		CPlayer.inventorytics = 0;
		if ( CPlayer.mo.InvSel )
		{
			DrawInventoryIcon(CPlayer.mo.InvSel,(-24,-24),DI_ITEM_CENTER,0.8);
			if ( CPlayer.mo.InvSel.amount > 1 ) DrawString(mTinyFont,FormatNumber(CPlayer.mo.InvSel.amount,0,5),(-9,-(9+mTinyFont.mFont.GetHeight())),DI_TEXT_ALIGN_RIGHT,alpha:0.8);
			DrawImage("selectbo",(-24,-24),DI_ITEM_CENTER,0.25);
		}
	}

	override void DrawAutomapHUD( double ticFrac )
	{
		let scale = GetHUDScale();
		double textdist = 8./scale.Y;
		int height = SmallFont.GetHeight();
		int SCREENWIDTH = screen.GetWidth();
		int crdefault = Font.CR_GREY;
		int highlight = Font.CR_UNTRANSLATED;
		BeginHUD();
		// Draw timer
		let y = textdist;
		let width = SmallFont.StringWidth("00:00:00");
		if ( am_showtime )
		{
			DrawString(mSmallFont,level.TimeFormatted(),(-textdist-width,y),0,crdefault);
			y += height;
		}
		if ( am_showtotaltime ) DrawString(mSmallFont,level.TimeFormatted(true),(-textdist-width,y),0,crdefault);
		y = textdist;
		// Draw room count
		if ( am_showsecrets )
		{
			DrawString(mSmallFont,String.Format("%s\34%c %d/%d",Stringtable.Localize("$AM_ROOMS"),crdefault+65,level.found_secrets,level.total_secrets),(textdist,y),0,highlight);
			y += height;
		}
		// Draw item count
		if ( am_showitems ) DrawString(mSmallFont,String.Format("%s\34%c %d/%d",Stringtable.Localize("$AM_ITEMS"),crdefault+65,level.found_items,level.total_items),(textdist,y),0,highlight);
	}
	override void NewGame()
	{
		Super.NewGame();
		HealthInter.Reset(0);
	}

	override void Tick()
	{
		Super.Tick();
		HealthInter.Update(CPlayer.Health);
		if ( LastSelected && (LastSelected != CPlayer.mo.InvSel) )
			S_Sound("item/select",CHAN_6);
		LastSelected = CPlayer.mo.InvSel;
	}
}
